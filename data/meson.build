subdir('icons')
subdir('appdata')

service_conf = configuration_data()
service_conf.set('bindir', bindir)
configure_file(
  input: 'surf.tangent.Tangent.service',
  output: 'surf.tangent.Tangent.service',
  configuration: service_conf,
  install_dir: join_paths(get_option('datadir'), 'dbus-1/services')
)

desktop_file = i18n.merge_file(
  input: 'surf.tangent.Tangent.desktop',
  output: 'surf.tangent.Tangent.desktop',
  type: 'desktop',
  po_dir: '../po',
  install: true,
  install_dir: join_paths(get_option('datadir'), 'applications')
)

desktop_utils = find_program('desktop-file-validate', required: false)
if desktop_utils.found()
  test('Validate desktop file', desktop_utils,
    args: [desktop_file]
  )
endif

install_data('surf.tangent.Tangent.gschema.xml',
  install_dir: join_paths(get_option('datadir'), 'glib-2.0/schemas')
)

compile_schemas = find_program('glib-compile-schemas', required: false)
if compile_schemas.found()
  test('Validate schema file', compile_schemas,
    args: ['--strict', '--dry-run', meson.current_source_dir()]
  )
endif
